# Rushy Panchal
# ~/.zshrc is only sourced for interactive shells.

# For profiling zsh, see: https://unix.stackexchange.com/a/329719/27109.
# Execute 'ZSH_PROFILE_RC=true zsh -i -c exit' and run 'zprof' to get the details.
if [[ -n "${ZSH_PROFILE_RC+1}" ]]; then
    zmodload zsh/zprof
fi

if [[ $TERM == "dumb" ]]; then
    # Set up a basic prompt.
    autoload -U colors
    autoload -Uz promptinit
    setopt PROMPT_SUBST
    PROMPT_CHAR="Î»"
    PROMPT='%F{39}%m%f:%F{yellow}%~%f %F{214}$PROMPT_CHAR%f '

    # Skip all other configuration, which depends on there being a smarter
    # terminal.
    return
fi

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
    source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

### Completions ###
autoload -U compinit

# Only load compinit once per day, see
# https://gist.github.com/ctechols/ca1035271ad134841284#gistcomment-2308206.
for dump in ~/.zcompdump(N.mh+24); do
    compinit
done

compinit -C

# menu-style completion
zstyle ':completion:*' menu select

# Emacs-style key bindings
bindkey -e
bindkey "^[[1;5C" forward-word
bindkey "^[[1;5D" backward-word

# search forwards/backwards in history
bindkey "^[[A" history-beginning-search-backward
bindkey "^[[B" history-beginning-search-forward

# History settings
HISTFILE="$HOME/.zsh_history"
HISTSIZE=1000000
SAVEHIST=1000000
setopt EXTENDED_HISTORY       # record timestamp of command in HISTFILE
setopt HIST_EXPIRE_DUPS_FIRST # delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt HIST_IGNORE_DUPS       # ignore duplicated commands history list
setopt HIST_SAVE_NO_DUPS	  # do not save duplicated commands in HISTFILE
setopt HIST_REDUCE_BLANKS     # remove superfluous blanks from each command
setopt HIST_VERIFY            # show command with history expansion to user before running it
setopt APPEND_HISTORY 	      # append to the history file, don't overwrite it
setopt INC_APPEND_HISTORY     # add commands to HISTFILE immediately in order of execution
setopt SHARE_HISTORY          # share command history data

# Use fzf for fuzzy history search.
if command -v fzf >/dev/null; then
    source <(fzf --zsh)
    # Use ctrl-y to copy the command when searching history.
    export FZF_CTRL_R_OPTS="
    --bind 'ctrl-y:execute-silent(echo -n {2..} | pbcopy)+abort'
    --color header:italic
    --header 'Press CTRL-Y to copy command into clipboard'"
    # Preview file content using bat (https://github.com/sharkdp/bat)
    export FZF_CTRL_T_OPTS="
    --walker-skip .git,node_modules,target
    --preview 'bat -n --color=always {}'
    --bind 'ctrl-/:change-preview-window(down|hidden|)'"
fi

### User configuration ###
export CLICOLOR=1

export PAGER='less -R'
if command -v moor >/dev/null; then
    export PAGER='moor'
    alias less="$PAGER"

fi
if command -v bat >/dev/null; then
    alias bat="bat --plain"
    # ...This hacky mess is recommended by bat.
    # See: https://github.com/sharkdp/bat?tab=readme-ov-file#man.
    export MANPAGER="sh -c 'awk '\''{ gsub(/\x1B\[[0-9;]*m/, \"\", \$0); gsub(/.\x08/, \"\", \$0); print }'\'' | bat -p -lman'"
fi

# Load functions.
zcompile ~/.sh_functions
source ~/.sh_functions

# Aliases
alias reload="exec zsh"
alias grep="grep --color=auto"
alias edit="$EDITOR"
alias view="view_file"

case `uname` in
    Darwin)
	alias  ls='ls -a -G'
	export LSCOLORS=HxfxBxdx
    ;;
    Linux)
	alias ls='ls -a --color=auto'
	export LS_COLORS='no=00;36:di=01;37:ex=01;31:ln=00;35'
	export LESS_TERMCAP_mb=$'\E[1;31m'     # begin bold
	export LESS_TERMCAP_md=$'\E[1;36m'     # begin blink
	export LESS_TERMCAP_me=$'\E[0m'        # reset bold/blink
	export LESS_TERMCAP_so=$'\E[01;44;33m' # begin reverse video
	export LESS_TERMCAP_se=$'\E[0m'        # reset reverse video
	export LESS_TERMCAP_us=$'\E[1;32m'     # begin underline
	export LESS_TERMCAP_ue=$'\E[0m'        # reset underline
	export GROFF_NO_SGR=1                  # for konsole and gnome-terminal
    ;;
    FreeBSD)
	# do nothing
    ;;
esac

# Environment variables (never committed).
if [ -f ~/.env_variables ]; then
    source ~/.env_variables
fi

# Local Configuration (never commited).
if [ -f ~/.zshrc.local ]; then
    source ~/.zshrc.local
fi

# iterm2 and ghostty integration.
# TODO(rushy_panchal): Remove this when no longer using iTerm2.
# if [ -f "${HOME}/.iterm2_shell_integration.zsh" ]; then
#     source "${HOME}/.iterm2_shell_integration.zsh"
# fi
if [ -n "${GHOSTTY_RESOURCES_DIR}" ]; then
    source "${GHOSTTY_RESOURCES_DIR}/shell-integration/zsh/ghostty-integration"
fi

# Mise, the package manager. See: https://mise.jdx.dev/.
eval "$(mise activate zsh)"

# Enable the powerlevel10k theme.
if [[ -f "$DOTFILES/zsh/powerlevel10k/powerlevel10k.zsh-theme" ]]; then
    source "$DOTFILES/zsh/powerlevel10k/powerlevel10k.zsh-theme"
fi

# To customize prompt, run `p10k configure` or edit p10k.zsh.
if [[ -f "$DOTFILES/zsh/p10k.zsh" ]]; then
     source "$DOTFILES/zsh/p10k.zsh"
fi

if [[ -n "${ZSH_PROFILE_RC+1}" ]]; then
    zprof
fi
